?domain: huasons\.nownews\.com

?path: /n/.*

title: //div[@itemprop="headline"]

body: //div[has-class("story_content")]

<figure>: //div[has-class("story_photo")]//img/..
<figcaption>: $@/h3
image_url: $@/../img/@src
@prepend_to($body): $@/..

@prepend(<hr>): $body//div[has-class("page_nav")]

@remove: $body//p[has-class("bzkeyword")]

@match("\\d.*\\d"): //div[@id="reporter_info"]/p/a[3]/@href
$date
@match("\\d.*\\d"): //div[@id="reporter_info"]/p/text()[4]
@append_to($date)
@wrap(<div>, id, "date")
@datetime(+8): $@
published_date

author: //head//meta[@property="og:site_name"]/@content



?domain: nownews\.com

?path: /n/.*

description: //head//meta[@property="og:description"]/@content

body: //div[has-class("body")]

$feapho: //div[has-class("featured-photo")]
@remove: $feapho//noscript
@wrap(<figure>): $feapho/a/img
$feaalt: $feapho/div/text()
@wrap(<figcaption>): $feaalt
@append_to($feapho/a/figure): $@
@prepend_to($body): $feapho//figure

image_url: //head//meta[@property="og:image"][contains(@content, "http")][1]/@content



?path: /v/.*

body: //div[has-class("body")]

image_url: //head//meta[@property="og:image"][contains(@content, "http")][1]/@content

@set_attrs(width, 640, height, 360): //div[has-class("featured-photo")]/iframe
@prepend_to($body)

<img>: //head//meta[@property="og:image"][contains(@content, "http")][1]
@wrap(<figure>)
@set_attr(src, @content)
cover



?true

title: //h1[has-class("content-title")]

author: //head//meta[@property="og:site_name"]/@content

@match("[0-9].*[0-9]"): //div[has-class("news-info")]/text()
@replace(" / ", " ")
@replace("\\.", "/")
@wrap(<div>, id, "date")
@datetime(+8): $@
published_date